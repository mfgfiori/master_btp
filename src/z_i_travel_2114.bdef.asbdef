managed; // implementation in class zbp_i_travel_2114 unique;
//strict ( 2 ); //Uncomment this line in order to enable strict mode 2. The strict mode has two variants (strict(1), strict(2)) and is prerequisite to be future proof regarding syntax and to be able to release your BO.

define behavior for z_i_travel_2114 alias Travel
implementation in class zbp_i_travel_2114 unique
persistent table ztb_travel_2114
with additional save
lock master
authorization master ( instance )
etag master last_changed_at
{
  // Campos de lecturas/ campos administrativos
  //field ( readonly ) travel_id;
  field ( readonly ) last_changed_at, last_changed_by, created_by, created_at;
  // Campos Obligatorios
  field ( mandatory : create ) travel_id, agency_id, overall_status, booking_fee, currency_code;
  //Dynamic field Control
  field ( features : instance ) travel_id, overall_status;

  create;
  update;
  delete;

  //Validaciones
  validation validateCustomer on save { create; field customer_id; }
  validation validateDates on save { create; field begin_date, end_date; }
  validation validateStatus on save { create; field overall_status; }

  //Acciones
  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;

  action createTravelByTemplate result [1] $self;

  association _Booking { create ( features : instance ); }

  mapping for ztb_travel_2114 corresponding;
}

define behavior for z_i_booking_2114 alias Booking
implementation in class zbp_i_booking_2114 unique
persistent table ztb_booking_2114
lock dependent by _Travel
authorization dependent by _Travel
etag master last_changed_at
{
  // Campos de lecturas/ campos administrativos
  field ( readonly ) travel_id;
  // Campos Obligatorios
  field ( mandatory : create ) booking_id, carrier_id, connection_id, flight_date, booking_status;
  update;
  delete;

  //Validaciones
  validation validateStatus on save { create; field booking_status; }

  //Determinaciones
  determination calculateTotalFlightPrice on modify { field flight_price, currency_code; }

  association _Travel { }
  association _BookingSupplement { create ( features : instance ); }

  mapping for ztb_booking_2114 corresponding;
}

define behavior for Z_I_BOOKSUPPL_2114 alias Supplement
implementation in class zbp_i_booksuppl_2114 unique
with unmanaged save
lock dependent by _Travel
authorization dependent by _Travel
etag master last_changed_at
{
  field ( readonly ) travel_id, booking_id;
  field ( mandatory : create ) supplement_id, price;
  update;
  delete;

  //Determinaciones
  determination calculateTotalSupplimnPrice on modify { field price, currency_code; }

  association _Travel { }
  association _Booking { }

//  mapping for  ztb_booksuppl_21 corresponding
//  {
//    travel_id = travel_id;
//    booking_id = booking_id;
//    booking_supplement_id = booking_supplement_id;
//    supplement_id = supplement_id;
//    price =  price;
//    currency_code = currency_code;
//    last_changed_at = last_change_at;
//  }

//  mapping for ztb_booksuppl_21 corresponding;
}